plugins {
    id 'io.micronaut.build.internal.server-module'
    alias(libs.plugins.micronaut.miniapp)
    alias(libs.plugins.micronaut.aot)
}

description = """
A lightweight server which runs a VM responsible for hosting
test resource resolvers.
This server must be used in conjunction with the test resources
client.
"""

dependencies {
    implementation(mn.micronaut.http.server.netty)
    implementation(mn.reactor)
    implementation(project(':test-resources-core'))
    implementation(project(':test-resources-embedded'))
    implementation(project(':test-resources-testcontainers'))
    runtimeOnly(mn.logback)
    runtimeOnly(mn.micronaut.management)
    runtimeOnly(mn.micronaut.serde.jackson)

    testImplementation(project(':test-resources-client'))
    testRuntimeOnly(project(':test-resources-kafka'))

    // For logback conversion
    aotPlugins("ch.qos.logback:logback-classic:1.3.0-alpha14")
    aotPlugins("org.fusesource.jansi:jansi:1.18")
    aotPlugins(mn.snakeyaml)
}

application {
    mainClass = "io.micronaut.testresources.server.Application"
}

micronaut {
    aot {
        cacheEnvironment.set(true)
        optimizeServiceLoading.set(true)
        optimizeClassLoading.set(true)
        convertYamlToJava.set(true)
        precomputeOperations.set(true)
        deduceEnvironment.set(true)
        replaceLogbackXml.set(true)
    }
}

micronautBuild {
    binaryCompatibility {
        // binary checks work on the wrong jar file
        enabled = false
    }
}
